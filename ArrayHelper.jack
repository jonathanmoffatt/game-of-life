class ArrayHelper {
    function Array getEmptyGrid() {
        var Array rows, columns;
        var int i;
        let i = 0;
        let rows = ArrayHelper.getEmptyArray();
        while (i < 32) {
            let columns = ArrayHelper.getEmptyArray();
            let rows[i] = columns;
            let i = i + 1;
        }
        return rows;
    }

    function Array getEmptyArray() {
        var Array empty;
        let empty = Array.new(32);
        do ArrayHelper.clearGridRow(empty);
        return empty;
    }

    function void clearGridRow(Array gridRow) {
        var int i;
        let i = 0;
        while (i < 32) {
            let gridRow[i] = false;
            let i = i + 1;
        }
        return;
    }

    function void clearGrid(Array grid) {
        var int row;
        let row = 0;
        while (row < 32) {
            do ArrayHelper.clearGridRow(grid[row]);
            let row = row + 1;
        }
        return;
    }

    function void setArrayCell(Array cells, int x, int y, boolean alive) {
        var Array row;
        let row = cells[y];
        let row[x] = alive;
        return;
    }

    function void setArrayCells(Array cellGrid, int x, int y, String cells) {
        var int i;
        let i = 0;
        while (i < cells.length()) {
            // a - (char 45) will leave the cell blank, anything else will set it
            do ArrayHelper.setArrayCell(cellGrid, x + i, y, ~(cells.charAt(i) = 45));
            let i = i + 1;
        }
        return;
    }

    function void copyGrid(Array source, Array target) {
        var Array sourceRow, targetRow;
        var int x, y;
        let y = 0;
        while (y < 32) {
            let sourceRow = source[y];
            let targetRow = target[y];
            let x = 0;
            while (x < 32) {
                let targetRow[x] = sourceRow[x];
                let x = x + 1;
            }
            let y = y + 1;
        }
        return;
    }

    function void cleanUp(Array cells) {
        var Array row;
        var int i;
        while (i < 32) {
            let row = cells[i];
            do row.dispose();
            let i = i + 1;
        }
        do cells.dispose();
        return;
    }

}