/*
    Array utility functions
*/
class ArrayHelper {

    // gets a two dimensional 32x32 array populated with false in every element
    function Array getEmptyGrid() {
        var Array rows, columns;
        var int i;
        let i = 0;
        let rows = ArrayHelper.getEmptyArray();
        while (i < 32) {
            let columns = ArrayHelper.getEmptyArray();
            let rows[i] = columns;
            let i = i + 1;
        }
        return rows;
    }

    // gets a 32 element array populated with false in every element
    function Array getEmptyArray() {
        var Array empty;
        let empty = Array.new(32);
        do ArrayHelper.clearGridRow(empty);
        return empty;
    }

    // clears a 32 element array by setting every element to false
    function void clearGridRow(Array gridRow) {
        var int i;
        let i = 0;
        while (i < 32) {
            let gridRow[i] = false;
            let i = i + 1;
        }
        return;
    }

    // clears a two dimensional 32 x 32 array by setting every element to false
    function void clearGrid(Array grid) {
        var int row;
        let row = 0;
        while (row < 32) {
            do ArrayHelper.clearGridRow(grid[row]);
            let row = row + 1;
        }
        return;
    }

    // sets the status of the cell at x,y to either alive or dead
    function void setArrayCell(Array cells, int x, int y, boolean alive) {
        var Array row;
        let row = cells[y];
        let row[x] = alive;
        return;
    }

    // sets cells starting at x,y and travelling horizontally using a string shorthand in "---x-x-xx" style,
    // where "-" represents a dead cell and "x" represents a live one
    function void setArrayCells(Array cellGrid, int x, int y, String cells) {
        var int i;
        let i = 0;
        while (i < cells.length()) {
            // a - (char 45) will leave the cell blank, anything else will set it
            do ArrayHelper.setArrayCell(cellGrid, x + i, y, ~(cells.charAt(i) = 45));
            let i = i + 1;
        }
        return;
    }

    // copies values from the source 32 x 32 two dimensional array to the target array
    function void copyGrid(Array source, Array target) {
        var Array sourceRow, targetRow;
        var int x, y;
        let y = 0;
        while (y < 32) {
            let sourceRow = source[y];
            let targetRow = target[y];
            let x = 0;
            while (x < 32) {
                let targetRow[x] = sourceRow[x];
                let x = x + 1;
            }
            let y = y + 1;
        }
        return;
    }

    // calls dispose on all arrays in a two dimensional 32 x 32 array
    function void cleanUp(Array cells) {
        var Array row;
        var int i;
        while (i < 32) {
            let row = cells[i];
            do row.dispose();
            let i = i + 1;
        }
        do cells.dispose();
        return;
    }

}