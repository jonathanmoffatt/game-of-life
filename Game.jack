class Game {
    field Array currentIteration, nextIteration;

    constructor Game new() {
        return this;
    }

    method void run() {
        var char key;
        var boolean quit, refresh;
        var int menu, currentMenu;

        let currentIteration = ArrayHelper.getEmptyGrid();
        let menu = 1;
        let currentMenu = 0;

        let quit = false;
        while (~quit) {
            let refresh = ~(menu = currentMenu);
            if ((menu = 1) & refresh) { do Menu.showMainMenu(); }
            if ((menu = 2) & refresh) { do Menu.showExamplesMenu(); }
            let currentMenu = menu;
            let key = Game.waitForKeyPressed();

            // E key: examples menu
            if ((key = 69) & (menu = 1)) { let menu = 2; }

            // L key: still lifes
            if ((key = 76) & (menu = 2)) {
                do ArrayHelper.clearGrid(currentIteration);
                do Examples.buildStillLifes(currentIteration, 12);
                do Grid.fillFromArray(currentIteration);
            } 

            // O key: oscillators
            if ((key = 79) & (menu = 2)) {
                do Menu.showOscillatorsMenu();
                do Examples.buildOscillators(currentIteration);
                do Grid.fillFromArray(currentIteration);
                let menu = 4;
            }

            // Q key: quit or move back to previous menu
            if (key = 81) {
                if (menu = 1) { let quit = true; } 
                if (menu = 2) { let menu = 1; }
                if (menu = 4) { let menu = 2; }
            }
            do Game.waitForKeyReleased(key);
        }

        do Menu.showFarewellMessage();
        return;
    }

    method void dispose() {
        do ArrayHelper.cleanUp(currentIteration);
        do Memory.deAlloc(this);
        return;
    }

    function char waitForKeyPressed() {
        var char key;
        let key = 0;
        while (key = 0) {
            let key = Keyboard.keyPressed();
        }
        return key;
    }

    function void waitForKeyReleased(char key) {
        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }
        return;
    }
}