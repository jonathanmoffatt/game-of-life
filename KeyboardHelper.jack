/*
    Keyboard utility functions
*/
class KeyboardHelper {

    // waits for a key to be pressed
    function char waitForKeyPressed() {
        var char key;
        let key = 0;
        while (key = 0) {
            let key = Keyboard.keyPressed();
        }
        return key;
    }

    // waits for a key to be released
    function void waitForKeyReleased(char key) {
        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }
        return;
    }

    // waits for the specified time period, or for a keypress (whichever comes first)
    function void waitForTimePeriodOrKeyPress(int timeInMilliseconds) {
        var int time;
        let time = 0;
        while (time < timeInMilliseconds) {
            do Sys.wait(10);
            if (~(Keyboard.keyPressed() = 0)) {
                return;
            }
            let time = time + 10;
        }
        return;
    }
}